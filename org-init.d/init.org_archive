#    -*- mode: org -*-


Archived entries from file c:/Users/70340930/.emacs.d/org-init.d/init.org

* éæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒª
** pukiwiki-mode                                                   :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-11-16 Sun 05:24
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: éæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
#+BEGIN_SRC emacs-lisp +n
(my-safe-require 'http)
(lazyload (pukiwiki-edit pukiwiki-index pukiwki-edit-url) "pukiwiki-mode"
  (setq pukiwiki-auto-insert t)
  (setq pukiwiki-site-list
        '(("bibouroku" "http://www.kaichan.mydns.jp/~kai/pukiwiki/index.php" nil utf-8-unix)
          ("gavo" "http://www.gavo.t.u-tokyo.ac.jp/members-only/pukiwiki/index.php" nil utf-8-unix)
          ("minerva" "http://minerva.gavo.t.u-tokyo.ac.jp/wiki/index.php" nil utf-8-unix)
          ("eeic09" "http://eeic09.dip.jp/index.php" nil utf-8-unix)
          ("WebPhantom" "http://ningyokan.nisfan.net/wiki/webpb/index.php" nil euc-jp)
          ("disgaea4" "http://alphawiki.net/disgaea4/index.php" nil euc-jp)))
  ;; Proxy server
  (defun my-http-proxy-enable ()
    (interactive)
    (setq http-proxy-server "localhost")
    (setq http-proxy-port 18080))
  (defun my-http-proxy-disable ()
    (interactive)
    (setq http-proxy-server nil)
    (setq http-proxy-port nil))
  ;; ãƒ­ãƒ¼ã‚«ãƒ«ã«ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä¿å­˜ã™ã‚‹
  (setq pukiwiki-directory (locate-user-emacs-file "pukiwiki-save"))
  (setq pukiwiki-save-post-data t)
  ;; pukiwiki-edit-mode ã§è¡Œã‚’æŠ˜ã‚Šè¿”ã•ãªã„
  )
(add-hook 'pukiwiki-edit-mode '(lambda ()
                                 (toggle-truncate-lines 1)
                                 (orgtbl-mode 1)))
#+END_SRC

** mu4e.el                                                         :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-11-16 Sun 06:05
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: éæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
æ–°ã—ã„ãƒ¡ãƒ¼ãƒ©ãƒ¼ï¼
#+BEGIN_SRC emacs-lisp +n
(my-safe-require 'mu4e
  ;; åŸºæœ¬è¨­å®š
  (setq mu4e-headers-date-format "%Y/%m/%d (%a)")
  (setq mu4e-view-prefer-html nil)  ;; html ã‚’ html ã®ã¾ã¾ã§è¡¨ç¤ºã—ãªã„
  (setq mu4e-view-show-addresses t)  ;; é€ä¿¡è€…ãªã©ã®ãƒ¡ãƒ¼ãƒ«ã‚¢ãƒ‰ãƒ¬ã‚¹ã‚’è¡¨ç¤ºã™ã‚‹
  (setq mu4e-html2text-command "w3m -dump -T text/html")  ;; html ã‚’ãƒ†ã‚­ã‚¹ãƒˆè¡¨ç¤º
  (setq mu4e-split-view 'vertical)  ;; ãƒ˜ãƒƒãƒ€ãƒãƒƒãƒ•ã‚¡ã®åˆ†å‰²æ–¹æ³•ï¼å·¦å³ã«åˆ†å‰²ã™ã‚‹
  (setq mu4e-headers-visible-columns 100)  ;; ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’é–‹ã„ãŸæ™‚ã®ãƒ˜ãƒƒãƒ€ãƒãƒƒãƒ•ã‚¡ã®å¤§ãã•
  (setq mu4e-show-images t)  ;; æ·»ä»˜ã•ã‚ŒãŸç”»åƒã‚’ã‚¤ãƒ³ãƒ©ã‚¤ãƒ³è¡¨ç¤ºã™ã‚‹
  (setq mu4e-view-image-max-width 800)  ;; ç”»åƒã®æœ€å¤§å¹…ï¼å˜ä½ã¯ãŠãã‚‰ããƒ”ã‚¯ã‚»ãƒ«
  (set-face-foreground 'mu4e-title-face "deep pink")  ;; ã‚¿ã‚¤ãƒˆãƒ«ç”¨ã®ãƒ•ã‚§ã‚¤ã‚¹
  (setq mu4e-use-fancy-chars nil) ;; ASCII æ–‡å­—ä»¥å¤–ã‚’ä½¿ã£ã¦è¡¨ç¤ºã—ãªã„
  (setq mu4e-headers-fields       ;; human-date ã‚’å°‘ã—åºƒã’ã‚‹
     '((:human-date . 16) (:flags . 6) (:from . 22) (:subject)))
  (define-key mu4e-headers-mode-map (kbd "m") 'mu4e-headers-mark-for-something) ;; ãƒãƒ¼ã‚¯ã—ã‚„ã™ã„ã‚ˆã†ã«ã‚­ãƒ¼ãƒã‚¤ãƒ³ãƒ‰å¤‰æ›´
  (define-key mu4e-headers-mode-map (kbd "M") 'mu4e-headers-mark-for-move)
  ;; ãƒ•ã‚©ãƒ«ãƒ€è¨­å®š
  (setq mu4e-maildir       "~/Maildir"        ;; top-level Maildir
        mu4e-sent-folder   "/gavo/INBOX.Sent"      ;; folder for sent messages
        mu4e-drafts-folder "/gavo/INBOX.Drafts"    ;; unfinished messages
        mu4e-trash-folder  "/gavo/INBOX.Trash"     ;; trashed messages
        mu4e-refile-folder "/gavo/INBOX.Archives") ;; saved messages
  ;; å—ä¿¡è¨­å®š
  (setq mu4e-get-mail-command "offlineimap"   ;; or fetchmail, or ...
        mu4e-update-interval 120)             ;; update every 5 minutes
  ;; ãƒ•ã‚©ãƒ«ãƒ€è¨­å®š
  (setq mu4e-search-results-limit 500) ;; -1 ã«ã™ã‚‹ã¨ç„¡åˆ¶é™
  (setq mu4e-maildir-shortcuts
        '(("/gavo/INBOX" . ?g)
          ("/gavo/INBOX.onsei-mail" . ?O)
          ("/kbkbkbkb1/INBOX" . ?k)
          ("/kbkbkbkb1/INBOX.mailing-list.Emacs-devel" . ?e)
          ("/kbkbkbkb1/INBOX.mailing-list.Emacs-orgmode" . ?)
          ("/cellphone/INBOX" . ?c)
          ))
  ;; é€ä¿¡è¨­å®š
  (setq auth-source-save-behavior nil) ; èªè¨¼æƒ…å ±ã‚’ ~/.authinfo ã«ä¿å­˜ã—ãªã„
  (add-to-list 'mu4e-user-mail-address-list "kbkbkbkb1@gmail.com")
  (add-to-list 'mu4e-user-mail-address-list "giovanni-franz@ezweb.ne.jp")
  (my-safe-require 'smtpmail
    (setq message-send-mail-function   'smtpmail-send-it
          smtpmail-stream-type 'ssl
          smtpmail-smtp-user           "kai"
          smtpmail-default-smtp-server "smtps.gavo.t.u-tokyo.ac.jp"
          smtpmail-smtp-server         "smtps.gavo.t.u-tokyo.ac.jp"
          smtpmail-smtp-service        465
          smtpmail-local-domain        "gavo.t.u-tokyo.ac.jp"))
  (define-key ctl-x-map (kbd "M") 'mu4e)
  (define-key ctl-x-map (kbd "m") 'mu4e-compose-new)
  ;; guide-key ã‚’ä½¿ã†
  (eval-after-load "guide-key"
    '(progn
       (defun guide-key/my-hook-function-for-mu4e-compose ()
         (dolist (key '("C-c" "C-c C-f"))
           (guide-key/add-local-guide-key-sequence key)))
       (add-hook 'mu4e-compose-mode-hook 'guide-key/my-hook-function-for-mu4e-compose)))
#+END_SRC
*** ãƒªãƒ³ã‚¯ã¸ç§»å‹•ï¼Œé–‹ã
ã‚«ãƒ¼ã‚½ãƒ«ã‚’ãƒªãƒ³ã‚¯ã¸ç§»å‹•ï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-view-my-goto-next-link (arg)
    "Go to next ARGth `mu4e-view-link-face'."
    (interactive "p")
    (let ((pt (point))
          (search-func (if (>= arg 0)
                           'next-single-property-change
                         'previous-single-property-change)))
      (dotimes (i (abs arg))
        (while (and (setq pt (funcall search-func pt 'face))
                    (not (eq 'mu4e-view-link-face
                             (get-text-property pt 'face))))
          nil)  ; do nothing
        (if pt
            (goto-char pt)
          (error (format "No link %s" (if (>= arg 0) "forward" "backward")))))
      ))
  (defun mu4e-view-my-goto-previous-link (arg)
    "Go to previous ARGth `mu4e-view-link-face'."
    (interactive "p")
    (mu4e-view-my-goto-next-link (- arg)))
  (define-key mu4e-view-mode-map (kbd "<tab>") 'mu4e-view-my-goto-next-link)
  (define-key mu4e-view-mode-map (kbd "<backtab>") 'mu4e-view-my-goto-previous-link)
#+END_SRC

ã‚«ãƒ¼ã‚½ãƒ«ä¸‹ã®ãƒªãƒ³ã‚¯ã‚’é–‹ãï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-view-my-get-link-at-point ()
    "Get mu4e link at point."
    ;; Not consider the case that link is at the beginning of buffer or at
    ;; the end of buffer
    (let ((pt (point)))
      (when (eq 'mu4e-view-link-face (get-text-property pt 'face))
        (if (eq 'mu4e-view-link-face (get-text-property (1- pt) 'face))
            ;; point is inside of link
            (buffer-substring-no-properties (previous-single-property-change pt 'face)
                                            (next-single-property-change pt 'face))
          ;; point is at the top of link
          (buffer-substring-no-properties pt (next-single-property-change pt 'face)))
        )))

  (defun mu4e-view-my-link-action-dwim (&optional arg)
    "Do action according to link at point. If link is url, open it with browser.
  If link is attachment, do the first action of `mu4e-view-attachment-actions'.
  If ARG is non-nil, select an action from `mu4e-view-attachment-actions'."
    (interactive "P")
    (let ((msg (or (mu4e-message-at-point)
                   (error "Message not found here")))
          (link (or (mu4e-view-my-get-link-at-point)
                    (error "Link not found here"))))
      (if (string-match mu4e~view-url-regexp link)
          (browse-url link)
        (let ((actionfunc (if arg
                              (mu4e-read-option "Action on attachment: "
                                                mu4e-view-attachment-actions)
                            (cdar mu4e-view-attachment-actions)))
              (attnum (or (mu4e-view-my-get-attachment-number link)
                          (mu4e~view-get-attach-num "Which attachment" msg))))
          (when (and actionfunc attnum)
            (funcall actionfunc msg attnum))))))

  (defun mu4e-view-my-get-attachment-number (name)
    "Get an attachment number from NAME of link."
    (save-excursion
      (goto-char (point-min))
      (when (and name
                 (re-search-forward (format "\\[\\([0-9]+\\)\\]%s" name) nil t))
        (string-to-int (match-string 1)))))

  (add-to-list 'mu4e-view-attachment-actions
               (cons "oopen-external" 'mu4e-view-open-attachment))
  (define-key mu4e-view-mode-map (kbd "RET") 'mu4e-view-my-link-action-dwim)
#+END_SRC
*** ç‰¹å®šã®ãƒ•ã‚©ãƒ«ãƒ€ã®ã¿ã®ãƒ¡ãƒ¼ãƒ«ã‚’å–å¾—ã™ã‚‹
ç‰¹å®šã®ãƒ•ã‚©ãƒ«ãƒ€ã®ãƒ¡ãƒ¼ãƒ«ã‚’ãƒ€ã‚¦ãƒ³ãƒ­ãƒ¼ãƒ‰ã™ã‚‹ã‚³ãƒãƒ³ãƒ‰ã‚’ä½œã£ãŸï¼ç†æƒ³çš„ã«ã¯
`mu4e-update-mail-and-index' ã®å¼•æ•°ã§æŒ™å‹•ãŒå¤‰ãˆã‚‰ã‚Œã‚‹ã‚ˆã†ã«ã—ãŸã»ã†ãŒã„ã„ã‹ã‚‚ï¼
ã•ã‚‰ã«æŠ½è±¡åŒ–ã™ã‚‹ãŸã‚ã«ï¼Œmaildir ã‹ã‚‰ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã¨ãƒ•ã‚©ãƒ«ãƒ€ã«å†™åƒã™ã‚‹é–¢æ•°ã¨ã‹æ›¸ã„
ãŸã»ã†ãŒã„ã„ã¨æ€ã‚ã‚Œã‚‹ï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-update-mail-and-index-partially (run-in-background)
    "Get a new mail by running `mu4e-get-mail-command'. If
  run-in-background is non-nil (or functional called with
  prefix-argument), run in the background; otherwise, pop up a
  window."
    (interactive "P")
    (unless mu4e-get-mail-command
      (mu4e-error "`mu4e-get-mail-command' is not defined"))
    (let* ((buf (unless run-in-background
                  (get-buffer-create mu4e~update-name)))
            (win (and buf (split-window (selected-window)
                            (- (window-height (selected-window)) 8))))
            (process-connection-type t)
            (command (mu4e~command-partial-update (mu4e-my-guess-maildir)))
            (proc (start-process-shell-command mu4e~update-name buf command)))
      (mu4e-message "Retrieving mail...")
      (when (window-live-p win)
        (with-selected-window win
          (switch-to-buffer buf)
          (set-window-dedicated-p win t)
          (erase-buffer)
          (insert "\n"))) ;; FIXME -- needed so output starts
      (set-process-sentinel proc
        (lambda (proc msg)
          (let* ((status (process-status proc))
                  (code (process-exit-status proc))
                  ;; sadly, fetchmail returns '1' when there is no mail; this is
                  ;; not really an error of course, but it's hard to distinguish
                  ;; from a genuine error
                  (maybe-error (or (not (eq status 'exit)) (/= code 0)))
                  (buf (process-buffer proc)))
            (message nil)
            ;; there may be an error, give the user up to 5 seconds to check
            (when maybe-error (sit-for 5))
            (mu4e-update-index)
            (when (buffer-live-p buf) (kill-buffer buf)))))
      ;; if we're running in the foreground, handle password requests
      (unless run-in-background
        (process-put proc 'x-interactive (not run-in-background))
        (set-process-filter proc 'mu4e~get-mail-process-filter))))

  (defun mu4e~command-partial-update (maildir)
    "Return a command for mu4e to update mail partially."
    (if (string-match "^/\\([^/\n]+\\)/\\([^/\n]+\\)" maildir)
        (let ((account (match-string 1 maildir))
              (folder  (utf7-encode (replace-regexp-in-string
                                     "\\." "/" (match-string 2 maildir)) 'imap)))
          (cond ((string= mu4e-get-mail-command "offlineimap")
                 (concat mu4e-get-mail-command
                         " -a " account
                         " -f " "'" folder "'"))
                (t (error "Not implemeqnted yet"))))
      (error (format "Invalid maildir: %s" maildir))))
#+END_SRC
*** æ¬¡ã®ãƒ•ã‚©ãƒ«ãƒ€ã«ç§»å‹•ã™ã‚‹ã‚³ãƒãƒ³ãƒ‰
ä½•ã‹ã¨ã‚ã‚Œã°ä¾¿åˆ©ã‹ã¨æ€ã£ã¦æ¬¡ã®ãƒ•ã‚©ãƒ«ãƒ€ã«ç›´æ¥ç§»å‹•ã™ã‚‹ã‚³ãƒãƒ³ãƒ‰ã‚’ä½œã£ãŸï¼å¤§ãã
ç§»å‹•ã™ã‚‹å ´åˆã¯ helm ã§ç§»å‹•ãŒã‚ˆã‚ã—ã„ï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-my-next-maildir (arg)
    "Move to a next maildir. This function get a current maildir
  from a message at point or last query."
    (interactive "p")
    (let* ((last-query (mu4e-last-query))
           (mdir (or (mu4e-my-guess-maildir)
                     (error "Cannot get a current maildir")))
           (mdir-list (or mu4e~maildir-list
                          (mu4e-get-maildirs)))
           (index (- (length mdir-list) (length (member mdir mdir-list))))
           (next-dir (nth (mod (+ index arg) (length mdir-list)) mdir-list)))
      (mu4e~headers-search-execute (concat "maildir:" next-dir) t)))
  (defun mu4e-my-previous-maildir (arg)
    "Move to a previous maildir."
    (interactive "p")
    (mu4e-my-next-maildir (- arg)))

  (define-key mu4e-headers-mode-map (kbd "M-n") 'mu4e-my-next-maildir)
  (define-key mu4e-headers-mode-map (kbd "M-p") 'mu4e-my-previous-maildir)
  (define-key mu4e-view-mode-map (kbd "M-n") 'mu4e-my-next-maildir)
  (define-key mu4e-view-mode-map (kbd "M-p") 'mu4e-my-previous-maildir)
#+END_SRC
*** é€ä¿¡ãƒ¡ãƒ¼ãƒ«ã‚’å…ƒãƒ¡ãƒ¼ãƒ«ã¨åŒã˜ãƒ•ã‚©ãƒ«ãƒ€ã«ã‚³ãƒ”ãƒ¼ã™ã‚‹
thunderbird ã®ã“ã®æ©Ÿèƒ½ãŒã¨ã£ã¦ã‚‚ä¾¿åˆ©ãªã®ã§ã¾ã­ã—ã¦ã¿ã‚‹ï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-my-save-message-in-same-folder ()
    "Save a sent message in same folder as a parent message.
  This function derives from `mu4e~setup-fcc-maybe'."
    (when mu4e-compose-parent-message
      (let* ((mpath (mu4e-message-field mu4e-compose-parent-message :path))
             (fccfile (and mpath
                           (concat (file-name-directory mpath)
                                   (mu4e~draft-message-filename-construct "S")))))
        (when fccfile
          (message-add-header (concat "Fcc: " fccfile "\n"))))))
  (add-hook 'mu4e-compose-mode-hook 'mu4e-my-save-message-in-same-folder)
#+END_SRC
ã¨ã‚Šã‚ãˆãšä½œã£ã¦ã¿ãŸãŒï¼Œ`mu4e-compose-mode-hook' ä¸­ã§
`mu4e-compose-parent-message' ãŒ nil ã«ãªã‚‹ï¼info ã§ã¯ä½¿ãˆã‚‹ã¨æ›¸ã„ã¦ã‚ã‚‹ã‚“ã 
ãŒï¼
*** å‹•çš„ã«ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’å¤‰æ›´ã™ã‚‹
[[info:mu4e#Compose%20hooks][info:mu4e#Compose hooks]] ã‚’å‚è€ƒã«ã—ã¦ï¼Œè¿”ä¿¡ï¼Œè»¢é€æ™‚ãªã©ã¯å…ƒã®ãƒ¡ãƒ¼ãƒ«ã«å¿œã˜ã¦
ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’å¤‰æ›´ã™ã‚‹ã“ã¨ãŒã§ããŸï¼`user-mail-address' ã‚’ç›´æ¥è¨­å®šã—ã¦ã„ã‚‹ã®
ã§æ³¨æ„ï¼

`compose-new' æ™‚ã«ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’è¨­å®šã™ã‚‹ï¼ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ã¯ `user-mail-address' ã‚’
åˆ©ç”¨ã™ã‚‹ãŒï¼Œå‰ç½®å¼•æ•°ã‚’ã¤ã‘ã¦å‘¼ã³å‡ºã™ã¨ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’é¸æŠã™ã‚‹ã“ã¨ãŒã§ãã‚‹ï¼
#+BEGIN_SRC emacs-lisp +n
  (defadvice mu4e-compose-new (before set-account (arg) activate)
    "Before executing `mu4e-commpose-new', set account information.
  If ARG is non-nil, select an account from `mu4e-user-mail-address-list'."
    (interactive "P")
    (if arg
        (mu4e-my-set-account-dynamically)
      (mu4e-my-set-account-dynamically user-mail-address)))
#+END_SRC

è¿”ä¿¡æ™‚ã«è¿”ä¿¡å…ƒã®ãƒ¡ãƒ¼ãƒ«ã®å ´æ‰€ã«å¿œã˜ã¦ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’åˆ‡ã‚Šæ›¿ãˆã‚‹ï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-my-change-account-for-maildir ()
    "Change my account according to maildir of a parent message."
    (when mu4e-compose-parent-message
      (let ((mdir (mu4e-message-field mu4e-compose-parent-message :maildir)))
        (cond ((string-match "^/gavo/" mdir)
               (mu4e-my-set-account-dynamically "kai@gavo.t.u-tokyo.ac.jp"))
              ((string-match "^/kbkbkbkb1/" mdir)
               (mu4e-my-set-account-dynamically "kbkbkbkb1@gmail.com"))
              ((string-match "^/cellphone/" mdir)
               (mu4e-my-set-account-dynamically "giovanni-franz@ezweb.ne.jp"))))))
  (add-hook 'mu4e-compose-pre-hook 'mu4e-my-change-account-for-maildir)
#+END_SRC

ãƒ¡ãƒ¼ãƒ«ã‚¢ãƒ‰ãƒ¬ã‚¹ã«å¿œã˜ã¦ã‚¢ã‚«ã‚¦ãƒ³ãƒˆæƒ…å ±ã‚’è¨­å®šã™ã‚‹ã‚³ãƒãƒ³ãƒ‰ï¼
#+BEGIN_SRC emacs-lisp +n
  (defun mu4e-my-set-account-dynamically (&optional address)
    "Set up an account according to ADDRESS.
If ADDRESS is nil, read ADDRESS from `mu4e-user-mail-address-list'."
    (interactive)
    (unless address
      (setq address (completing-read "Mail account: " mu4e-user-mail-address-list
                                     nil t nil nil user-mail-address)))
    (cond ((string= address "kai@gavo.t.u-tokyo.ac.jp")
           (setq mu4e-sent-folder   "/gavo/INBOX.Sent"
                 mu4e-drafts-folder "/gavo/INBOX.Drafts"
                 mu4e-trash-folder  "/gavo/INBOX.Trash"
                 mu4e-sent-messages-behavior  'sent
                 user-mail-address            address
                 smtpmail-stream-type         'ssl
                 smtpmail-smtp-user           "kai"
                 smtpmail-default-smtp-server "smtps.gavo.t.u-tokyo.ac.jp"
                 smtpmail-smtp-server         "smtps.gavo.t.u-tokyo.ac.jp"
                 smtpmail-smtp-service        465
                 smtpmail-local-domain        "gavo.t.u-tokyo.ac.jp"))
          ((string= address "kbkbkbkb1@gmail.com")
           (setq mu4e-drafts-folder "/kbkbkbkb1/[Gmail].ä¸‹æ›¸ã"
                 mu4e-sent-folder   "/kbkbkbkb1/[Gmail].é€ä¿¡æ¸ˆã¿ãƒ¡ãƒ¼ãƒ«"
                 mu4e-trash-folder  "/kbkbkbkb1/[Gmail].ã‚´ãƒŸç®±"
                 mu4e-sent-messages-behavior  'sent
                 user-mail-address            address
                 smtpmail-stream-type         'ssl
                 smtpmail-smtp-user           "kbkbkbkb1@gmail.com"
                 smtpmail-default-smtp-server "smtp.gmail.com"
                 smtpmail-smtp-server         "smtp.gmail.com"
                 smtpmail-smtp-service        465
                 smtpmail-local-domain        "gmail.com"))
          ((string= address "giovanni-franz@ezweb.ne.jp")
           (setq mu4e-drafts-folder "/cellphone/[Gmail].ä¸‹æ›¸ã"
                 mu4e-sent-folder   "/cellphone/[Gmail].é€ä¿¡æ¸ˆã¿ãƒ¡ãƒ¼ãƒ«"
                 mu4e-trash-folder  "/cellphone/[Gmail].ã‚´ãƒŸç®±"
                 mu4e-sent-messages-behavior  'sent
                 user-mail-address            address
                 smtpmail-stream-type         'ssl
                 smtpmail-smtp-user           "giovanni-franz@auone.jp"
                 smtpmail-default-smtp-server "smtp.gmail.com"
                 smtpmail-smtp-server         "smtp.gmail.com"
                 smtpmail-smtp-service        465
                 smtpmail-local-domain        "ezweb.ne.jp"))
          (t (error (format "Unknown mail address: %s" address)))))
#+END_SRC
*** ç‹¬è‡ªæ‹¡å¼µ
org-mode ã§ mu4e ã®ãƒ¡ãƒ¼ãƒ«ã¸ã®ãƒªãƒ³ã‚¯ã‚’ä½œæˆã§ãã‚‹ org-mu4eï¼
#+BEGIN_SRC emacs-lisp +n
  (my-safe-require 'org-mu4e)   ; org-mode ã¨ã®å”èª¿
  (add-hook 'mu4e-view-mode-hook 'toggle-truncate-lines)
#+END_SRC

helm ã§ mu4e ã®ãƒ•ã‚©ãƒ«ãƒ€ã‚’é¸æŠã™ã‚‹ï¼
#+BEGIN_SRC emacs-lisp +n
  (defvar helm-c-source-mu4e-maildir
    '((name . "mu4e-maildir")
      (candidates . mu4e-get-maildirs)
      (real-to-display . helm-c-source-mu4e-maildir-real-to-diaplay)
      (action . (("Jump to maildir" . mu4e~headers-jump-to-maildir)
                 ("Insert maildir" . insert)))
      (candidate-number-limit)
      (migemo))
    "Helm source for mu4e maildir.")

  (defun helm-c-source-mu4e-maildir-real-to-diaplay (maildir)
    ""
    (let ((count (with-temp-buffer
                   (shell-command (concat "mu find flag:unread maildir:"
                                          maildir " 2>/dev/null | wc -l")
                                  (current-buffer))
                   (replace-regexp-in-string "\n" "" (buffer-string)))))
      (if (> (string-to-int count) 0)
          (propertize (concat maildir " [" count "]") 'face 'mu4e-unread-face)
        maildir)))

  (defun helm-mu4e-maildir ()
    "Helm command for mu4e maildir."
    (interactive)
    (helm :sources   'helm-c-source-mu4e-maildir
              :buffer    "*helm-mu4e-maildir*"
              :preselect (mu4e-my-guess-maildir)))
  (define-key mu4e-main-mode-map (kbd "f") 'helm-mu4e-maildir)   ; smtpmail-send-queued-mail ã‚’ä¸Šæ›¸ã
  (define-key mu4e-headers-mode-map (kbd "f") 'helm-mu4e-maildir)
  (define-key mu4e-view-mode-map (kbd "f") 'helm-mu4e-maildir)

  (defun mu4e-my-guess-maildir ()
    "Guess a current maildir from a message at point or last search query.
  If the guess fails, return nil."
    (let ((last-query (mu4e-last-query))
          (maildir-regexp "maildir:\\([^\) \r\n\\]+\\)\\()\\| AND\\|\\\)?")) ; This may be wrong
      (or (ignore-errors (mu4e-field-at-point :maildir))
          (and last-query
               (string-match maildir-regexp last-query)
               (match-string 1 last-query)))))
#+END_SRC

helm ã§ãƒ¡ãƒ¼ãƒ«ã‚¢ãƒ‰ãƒ¬ã‚¹ã®è£œå®Œã‚’ã™ã‚‹ï¼ãƒ¡ãƒ¼ãƒ«ã‚¢ãƒ‰ãƒ¬ã‚¹ã¯
`mu4e~contacts-for-completion' ã«æ–‡å­—åˆ—ã®ãƒªã‚¹ãƒˆã§æ ¼ç´ã•ã‚Œã¦ã„ã‚‹ã®ã§ç°¡å˜ã«ã‹ã‘
ãŸï¼ã¯ã˜ã‚ã¦ history ã‚’ä½œã£ã¦ã¿ãŸãŒï¼Œã¾ãšã¾ãšå‹•ã„ã¦ã„ã‚‹ã¨æ€ã†ï¼
`mu4e-my-insert-contact-and-save-history' ã§ç›´æ¥å±¥æ­´ã‚’ä¿å­˜ã—ã¦ã„ã‚‹ã®ãŒå°‘ã—ãƒ€
ã‚µã‚¤æ°—ãŒã™ã‚‹ï¼
#+BEGIN_SRC emacs-lisp +n
  (defvar mu4e-my-contact-history nil "Contact history for mu4e")

  (defun mu4e-my-insert-contact-and-save-history (candidate)
    "Insert contact and save hisroty"
    (setq mu4e-my-contact-history (delete candidate mu4e-my-contact-history))
    (add-to-list 'mu4e-my-contact-history candidate)
    (insert candidate))

  (defvar helm-c-source-mu4e-my-contact-history
    '((name . "mu4e Contact History")
      (candidates . mu4e-my-contact-history)
      (action . mu4e-my-insert-contact-and-save-history)
      (migemo))
    "Helm source for mu4e contact history.")

  (defvar helm-c-source-mu4e-contact
    '((name . "mu4e Contact Completion")
      (candidates . mu4e~contacts-for-completion)
      (action . mu4e-my-insert-contact-and-save-history)
      (migemo))
    "Helm source for mu4e contact.")

  (defun helm-mu4e-contact ()
    "Helm command for mu4e contact."
    (interactive)
    (helm-other-buffer '(helm-c-source-mu4e-my-contact-history
                             helm-c-source-mu4e-contact)
                           "*helm-mu4e-conntact*"))
  (define-key mu4e-compose-mode-map (kbd "C-c c") 'helm-mu4e-contact)
#+END_SRC

æœ€å¾Œã®ã‚«ãƒƒã‚³ï¼
#+BEGIN_SRC emacs-lisp +n
  )
#+END_SRC

** org2blog.el                                                     :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-11-16 Sun 06:06
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: éæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
- [[https://github.com/punchagan/org2blog][punchagan/org2blog]]
org-mode å½¢å¼ã®ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å¤‰æ›ã— wordpress ã«æŠ•ç¨¿ã™ã‚‹ï¼marmalade ã‚ˆã‚Šã‚¤ãƒ³ã‚¹ãƒˆãƒ¼
ãƒ«ï¼
#+BEGIN_SRC emacs-lisp +n
(my-safe-require 'org2blog
  (setq org2blog/wp-tags-list '("Emacs" "WordPress"))
  (setq org2blog/wp-blog-alist
        `(("wordpress"
           :url "http://www.kaichan.mydns.jp/~kai/wordpress/xmlrpc.php"
           :username "kbkbkbkb1"
           :password "pass"
           :tags-as-categories nil)
          ))
  ;; my function
  (defun org2blog/wp-my-post-current-subtree ()
    "Preview current subtree."
    (interactive)
    (save-excursion
      ;; move to top level of the outline
      (ignore-errors
        (while (> (progn (outline-up-heading 1)
                         (funcall outline-level)) 1)))
      (let ((org2blog/wp-show-post-in-browser nil))
        (org2blog/wp-post-subtree))))
  (defun org2blog/wp-my-post-current-subtree-and-preview ()
    "Preview current subtree."
    (interactive)
    (save-excursion
      ;; move to top level of the outline
      (ignore-errors
        (while (> (progn (outline-up-heading 1)
                         (funcall outline-level)) 1)))
      (let ((org2blog/wp-show-post-in-browser 'show))
        (org2blog/wp-post-subtree))))   ; preview
  (defun org2blog/wp-my-post-current-subtree-and-publish ()
    "Post current subtree."
    (interactive)
    (save-excursion
      ;; move to top level of the outline
      (ignore-errors
        (while (> (progn (outline-up-heading 1)
                         (funcall outline-level)) 1)))
      (let ((org2blog/wp-show-post-in-browser nil))
        (org2blog/wp-post-subtree t)))) ; post

  ;; WP SyntaxHighlighter Evolved ã‚’ä½¿ã†æ™‚ç”¨ã®è¨­å®š
  ;; (setq org2blog/wp-use-sourcecode-shortcode t)
  ;; (setq org2blog/wp-sourcecode-default-params "")
  ;; (dolist (lang '("lisp" "clisp" "elisp" "emacs-lisp" "clojure" "scheme"))
  ;;   (add-to-list 'org2blog/wp-sourcecode-langs lang))
  )
#+END_SRC
** calfw.el                                                        :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-11-16 Sun 06:18
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: éæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
- [[http://d.hatena.ne.jp/kiwanami/20110107/1294404952][Emacsã§ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ã‚³ãƒ³ãƒãƒ¼ãƒãƒ³ãƒˆã‚’ä½œã£ãŸ - æŠ€è¡“æ—¥è¨˜ï¼ kiwanami]]
é«˜æ©Ÿèƒ½ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ï¼
#+BEGIN_SRC emacs-lisp +n
;;; (auto-install-from-url "https://github.com/kiwanami/emacs-calfw/raw/master/calfw.el")
;; æ—¥æœ¬ã®ç¥æ—¥é–¢é€£
;; (auto-install-from-url "http://www.meadowy.org/meadow/netinstall/export/799/branches/3.00/pkginfo/japanese-holidays/japanese-holidays.el")
(add-hook 'calendar-load-hook
          (lambda ()
            (my-safe-require 'japanese-holidays)
            (setq calendar-holidays
                  (append japanese-holidays local-holidays other-holidays))))
(setq mark-holidays-in-calendar t)
;; ç¥æ—¥ã®è¨­å®šã‚’å…ˆã«ã—ã¦ãªã„ã¨ã„ã‘ãªã„ã£ã½ã„
;; (lazyload (my-open-calendar) "calfw"
(lazyload (my-open-calendar) "calfw-cal"
  ;; iCalenderå½¢å¼ã¨é€£æº
  ;; (auto-install-from-url "https://github.com/kiwanami/emacs-calfw/raw/master/calfw-ical.el")
  (my-safe-require 'calfw-ical)

  ;; 2011-06-20 (Mon)
  ;; org-mode ã¨é€£æº
  ;; (auto-install-from-url "https://raw.github.com/kiwanami/emacs-calfw/master/calfw-org.el")
  (my-safe-require 'calfw-org)

  ;; calfw-gcal.el
  ;; ã‹ã‚‹ãµã‚ã‹ã‚‰ google ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ã‚’ç·¨é›†ã§ãã‚‹
  ;; (auto-install-from-url "https://github.com/myuhe/calfw-gcal.el/raw/master/calfw-gcal.el")
  (my-safe-require 'calfw-gcal
    (define-key cfw:calendar-mode-map (kbd "a") 'cfw:gcal-main)
    )

  ;; ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ã‚’é–‹ãé–¢æ•°
  (defun my-open-calendar ()
    (interactive)
    (cfw:open-calendar-buffer
     :view 'month
     :contents-sources
     (list
      (cfw:org-create-source "Seagreen4") ; color
      )))
  )
#+END_SRC
** google é–¢é€£                                                     :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-11-16 Sun 06:20
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: éæ¨™æº–ãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
- [[http://maarui.doorblog.jp/archives/6301428.html][ã¾ãã‚‹ã„ã—ã£ã½:emacsã®ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ã§google calendarã‚’èª­ã¿è¾¼ã‚€]]
ã‚«ãƒ¬ãƒ³ãƒ€ãƒ¼ã¸ã®ã‚¤ãƒ™ãƒ³ãƒˆè¿½åŠ ã¯ã§ããŸãŒï¼Œã‚¿ã‚¤ãƒ ã‚¾ãƒ¼ãƒ³ãŒ +0:00 ã«ãªã£ã¦ã„ã¾ã„æ™‚é–“
æŒ‡å®šãŒæ€ã„é€šã‚Šã«ãªã‚‰ãªã„ï¼ã‚¿ã‚¤ãƒ ã‚¾ãƒ¼ãƒ³ã‚’æŒ‡å®šã—ãŸã„ãŒã‚„ã‚Šæ–¹ãŒã‚ã‹ã‚‰ãªã„ï¼
#+BEGIN_SRC emacs-lisp +n
(my-safe-require 'icalendar)
(my-safe-require 'google-calendar
  (setq google-calendar-user "username")         ;;; GOOGLE USER
  (setq google-calendar-code-directory     ;;; PATH TO THE PYTHON CODE
        (expand-file-name "code"
                          (file-name-directory (locate-library "google-calendar"))))
  (setq google-calendar-directory      "~/tmp")               ;;; TEMPORARY DIRECTORY
  (setq google-calendar-url "http://www.google.com/calendar/ical/username%40gmail.com/private-96087f02e8d133b8d7bab6e5a0712574/basic.ics")  ;;; URL TO YOUR GOOGLE CALENDAR
  (setq google-calendar-auto-update t))
#+END_SRC
* å¤–éƒ¨ãƒ—ãƒ­ã‚°ãƒ©ãƒ ãŒå¿…è¦ãã†ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒª
** matlab.el
:PROPERTIES:
:ARCHIVE_TIME: 2014-05-29 Thu 18:21
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: å¤–éƒ¨ãƒ—ãƒ­ã‚°ãƒ©ãƒ ãŒå¿…è¦ãã†ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
- [[http://d.hatena.ne.jp/uhiaha888/20100815/1281888552][emacsã§matlab - uhiaha888ã®æ—¥è¨˜]]
#+BEGIN_SRC emacs-lisp
(autoload 'matlab-mode "matlab" "Enter MATLAB mode." t)
(autoload 'matlab-shell "matlab" "Interactive MATLAB mode." t)
(setq auto-mode-alist (delete '("\\.m\\'" . objc-mode) auto-mode-alist)) ; .m ãƒ•ã‚¡ã‚¤ãƒ«ãŒ object-c ã«è§£é‡ˆã•ã‚Œãªã„ã‚ˆã†ã«ã™ã‚‹
(add-to-list 'auto-mode-alist '("\\.m\\'" . matlab-mode))
(setq matlab-shell-command "/usr/local/bin/matlab"
      matlab-shell-command-swithes '("-nodesktop -v=glnx86")
      matlab-indent-level 4
      matlab-indent-function-body nil
      matlab-highlight-cross-function-variables t
      matlab-return-add-semicolon t
      matlab-show-mlint-warnings t
      mlint-programs '("/usr/local/matlab75/bin/glnx86/mlint")
      matlab-mode-install-path (list (expand-file-name "/usr/local/matlab75/")))
(autoload 'mlint-minor-mode "mlint" nil t)
(add-hook 'matlab-mode-hook (lambda ()
                              (mlint-minor-mode 1)
                              ;; config about face for mlint
                              (set-face-background 'linemark-go-face "gray40")
                              (set-face-background 'linemark-funny-face "red")
                              ))
;; mlint ã—ã‚ˆã†ã¨ã™ã‚‹ã¨ï¼Œ linemark.el ãŒå¿…è¦ã‚‰ã—ã„ãŒï¼Œrequire ã—ãŸã‚‰ã—ãŸã§
;; ãŠã‹ã—ã„ã®ã§ä½¿ã‚ãªã„ã‚ˆã†ã«ã—ã‚ˆã†ï¼
;; cedet ã‹ã‚‰ linemark.el ã ã‘ã‚³ãƒ”ãƒ¼ã—ã¦ãƒ­ãƒ¼ãƒ‰ã—ãŸã‚‰ä½•ã¨ã‹å‹•ãã‚ˆã†ã ï¼
(add-hook 'matlab-mode-hook (lambda () (auto-fill-mode 0)))
(add-hook 'matlab-shell-mode-hook 'ansi-color-for-comint-mode-on)
(add-hook 'matlab-shell-mode-hook
           (lambda () (setenv "LANG" "C")))
(eval-after-load "shell"
  '(define-key shell-mode-map [down] 'comint-next-matching-input-from-input))
(eval-after-load "shell"
  '(define-key shell-mode-map [up] 'comint-previous-matching-input-from-input))
(eval-after-load "matlab"
  '(progn
     (define-key matlab-mode-map (kbd "M-;") 'nil))) ; matlab-mode ã§ dwim-comment ã‚’ã¤ã‹ã†
     ;; (define-key matlab-shell-mode-map (kbd "<tab>") 'ac-complete)
     ;; (define-key matlab-shell-mode-map (kbd "C-<tab>") 'matlab-shell-tab)))

(defface ac-matlab-candidate-face
  '((t (:background "PaleGreen" :foreground "black")))
  "Face for matlab candidate."
  :group 'auto-complete)

(defface ac-matlab-selection-face
  '((t (:background "DarkGreen" :foreground "white")))
  "Face for matlab selected candidate."
  :group 'auto-complete)

(defun matlab-complete-symbol-list (&optional arg)
  (interactive "P")
  ;(matlab-navigation-syntax
    (let* ((prefix (if (and (not (eq last-command 'matlab-complete-symbol))
                            (member (preceding-char) '(?  ?\t ?\n ?, ?\( ?\[ ?\')))
                       ""
                     (buffer-substring-no-properties
                      (save-excursion (forward-word -1) (point))
                      (point))))
           (sem (matlab-lattr-semantics prefix)))
      (if (not (eq last-command 'matlab-complete-symbol))
          (setq matlab-last-prefix prefix
                matlab-last-semantic sem
                matlab-completion-search-state
                (cond ((eq sem 'solo)
                       '(matlab-solo-completions

                         matlab-find-user-functions
                         matlab-find-recent-variable))
                      ((eq sem 'boolean)
                       '(matlab-find-recent-variable
                         matlab-boolean-completions
                         matlab-find-user-functions
                         matlab-value-completions))
                      ((eq sem 'value)
                       '(matlab-find-recent-variable
                         matlab-find-user-functions
                         matlab-value-completions
                         matlab-boolean-completions))
                      ((eq sem 'property)
                       '(matlab-property-completions
                         matlab-find-user-functions
                         matlab-find-recent-variable
                         matlab-value-completions))
                      (t '(matlab-find-recent-variable
                           matlab-find-user-functions
                           matlab-value-completions
                           matlab-boolean-completions)))))

      (let ((allsyms (apply 'append
                            (mapcar (lambda (f) (funcall f prefix))
                                    matlab-completion-search-state))))
        (matlab-uniquafy-list allsyms))))

(defvar ac-source-matlab
  '((candidates
     . (lambda ()
         (matlab-complete-symbol-list)))
    (candidate-face . ac-matlab-candidate-face)
    (selection-face . ac-matlab-selection-face)
    ))

(defvar ac-source-matlab-functions nil
  "Souce for matlab functions.")
(setq ac-source-matlab-functions
      '((candidates . (list "zeros" "ones" "eye" "mean" "exp" "length" "save" "normpdf" "plot"
                            "size" "print" "sum" "prod" "inv" "diag" "rand" "randn" "linspace"
                            "logspace" "length" "any" "all" "find" "reshape" "meshgrid" "char"
                            "deblank" "double" "strcmp" "strncmp" "isletter" "isspace" "findstr"
                            "strrep" "int2str" "num2str" "str2num" "dec2hex" "dec2bin" "hex2dec"
                            "bin2dec" "mat2str" "eval" "tic" "toc" "figure" "hold" "subplot"
                            "fplot" "title" "xlabel" "ylabel" "text" "gtext" "grid" "axis"
                            "legend" "set" "get" "bar" "barh" "area" "pie" "hist" "stem"
                            "staris" "compass" "feather" "quiver" "contour" "ginput" "drawnow"
                            "image" "imagesc" "image" "imread" "fill" "fprintf" "fopen"
                            "fclose" "fgetl" "feof" "fwrite" "fread"))
        (document . ac-matlab-function-documentation)
        (symbol . "f")))

(defun ac-matlab-function-documentation (fnc)
  "Show document of matlab function."
  (condition-case nil
      (matlab-shell-collect-command-output (concat "help " fnc))
    (error "You need to run the command `matlab-shell' to read help!")))


(add-hook 'matlab-mode-hook
          (lambda ()
            (add-to-list 'ac-sources 'ac-source-matlab)
            (add-to-list 'ac-sources 'ac-source-matlab-functions)
            (dolist (source '(ac-source-features ac-source-functions
                                                 ac-source-variables ac-source-symbols))
              (setq ac-sources (delq source ac-sources)))
            (key-chord-define matlab-mode-map "df" 'matlab-shell-describe-command)
            (key-chord-define matlab-mode-map "dv" 'matlab-shell-describe-variable)
            (key-chord-define matlab-mode-map "AA" 'matlab-shell-apropos)
            ))

(add-hook 'matlab-shell-mode-hook
          (lambda ()
            (add-to-list 'ac-sources 'ac-source-matlab)
            (add-to-list 'ac-sources 'ac-source-matlab-functions)
            (dolist (source '(ac-source-features ac-source-functions
                                                 ac-source-variables ac-source-symbols))
              (setq ac-sources (delq source ac-sources)))
            (key-chord-define matlab-shell-mode-map "df" 'matlab-shell-describe-command)
            (key-chord-define matlab-shell-mode-map "dv" 'matlab-shell-describe-variable)
            (key-chord-define matlab-shell-mode-map "AA" 'matlab-shell-apropos)
            ))




;;; ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã˜ã‚ƒãªã„å ´æ‰€ã‹ã‚‰ comint-previous-input ã‚’
;;; å®Ÿè¡Œã—ãŸã¨ãï¼Œãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã«ç§»å‹•ã™ã‚‹
;;; 2011-06-30 (Thu)
(defadvice comint-previous-input
  (before comint-previous-input-anywhere activate)
  (if (not (comint-after-pmark-p))
      (end-of-buffer)))
#+END_SRC

** wl.el                                                           :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-05-29 Thu 18:31
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: å¤–éƒ¨ãƒ—ãƒ­ã‚°ãƒ©ãƒ ãŒå¿…è¦ãã†ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
Wanderlust ãƒ¡ãƒ¼ãƒ©ãƒ¼ï¼è¨­å®šã¯ .wl, .folders ã§ã™ã‚‹ï¼
#+BEGIN_SRC emacs-lisp
;; SSL/TLS ç”¨è¨¼æ˜æ›¸ã‚¹ãƒˆã‚¢ã®ãƒ‘ã‚¹
;(setq ssl-certificate-directory (concat dropbox-directory "/certs"))
(setq ssl-certificate-verification-policy 1)

(autoload 'wl "wl" "Wanderlust" t)
(autoload 'wl-other-frame "wl" "Wanderlust on new frame." t)
(autoload 'wl-draft "wl-draft" "Write draft with Wanderlust." t)
(setq wl-init-file (concat user-emacs-directory ".wl"))
(setq wl-folders-file (concat user-emacs-directory ".folders"))
#+END_SRC

** lookup.el                                                       :noexport:
:PROPERTIES:
:ARCHIVE_TIME: 2014-05-29 Thu 18:58
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: å¤–éƒ¨ãƒ—ãƒ­ã‚°ãƒ©ãƒ ãŒå¿…è¦ãã†ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
å¤šæ©Ÿèƒ½è¾æ›¸å¼•ããƒ—ãƒ­ã‚°ãƒ©ãƒ ï¼
#+BEGIN_SRC emacs-lisp
;; ã‚ªãƒ¼ãƒˆãƒ­ãƒ¼ãƒ‰ã®è¨­å®š
;; (autoload 'lookup "lookup" nil t)
;; (autoload 'lookup-region "lookup" nil t)
;; (autoload 'lookup-word "lookup" nil t)
;; (autoload 'lookup-select-dictionaries "lookup" nil t)
;; dictionary setting
(setq lookup-search-agents
             '((ndeb "~/dict/EIJIRO126-epwing")
               (ndeb "~/dict/LDOCE5")
               (ndeb "~/dict/KOJIEN6")
               (ndeb "~/dict/Wikip0503/epwing")
               ))
(setq lookup-default-dictionary-options
      '((:stemmer .  stem-english)))
(my-safe-require 'lookup
  ;; remove an inflected suffix
  ;(lookup-set-dictionary-options "ndeb:~/.emacs.d/dict/WORDNET/wordnet" ':stemmer 'stem-english) ; ã†ã¾ãã„ã‹ã‚“ï¼Ÿ

  ;; setting
  (setq lookup-window-height 8)
  (if (executable-find "mecab")
      (setq lookup-use-mecab t))

  ;; key bind
  (define-key mode-specific-map (kbd "d") 'lookup-pattern)
  (define-key mode-specific-map (kbd "D") 'lookup-select-dictionaries)
  )
#+END_SRC

** edbi.el
:PROPERTIES:
:ARCHIVE_TIME: 2014-11-16 Sun 06:09
:ARCHIVE_FILE: ~/.emacs.d/org-init.d/init.org
:ARCHIVE_OLPATH: å¤–éƒ¨ãƒ—ãƒ­ã‚°ãƒ©ãƒ ãŒå¿…è¦ãã†ãªãƒ©ã‚¤ãƒ–ãƒ©ãƒª
:ARCHIVE_CATEGORY: init
:END:
#+BEGIN_SRC emacs-lisp +n
;(my-safe-require 'edbi)
#+END_SRC


